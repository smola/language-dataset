// label to use for text actions
Label* action;

void set_ActionLabel(this TwoClickHandler*,  Label* label)
{
  action = label;
  action.Text = "";
}

Label* get_ActionLabel(this TwoClickHandler*)
{
  return action;
}

// GUI to use as inventory
GUI* interface_inv;

void set_InventoryGUI(this TwoClickHandler*,  GUI* invGui)
{
  interface_inv = invGui;
}

GUI* get_InventoryGUI(this TwoClickHandler*)
{
  return interface_inv;
}

// reversed control mode (left click to look)
bool classic = false;

void set_ReversedClicks(this TwoClickHandler*, bool reversed)
{
  classic = reversed;
}

bool get_ReversedClicks(this TwoClickHandler*)
{
  return classic;
}

static function TwoClickHandler::Close()
{
  if (interface_inv != null)
  {
    interface_inv.Visible = false;
  }
}

// popup distance from screen edge
int popup_distance = 0;

void set_PopupDistance(this TwoClickHandler*, int distance)
{
  popup_distance = distance;
}

int get_PopupDistance(this TwoClickHandler*)
{
  return popup_distance;
}

// popup distance proportional to GUI height
float popup_proportional = 0.75;

void set_PopupProportional(this TwoClickHandler*, float height)
{
  popup_proportional = height;
}

float get_PopupProportional(this TwoClickHandler*)
{
  return popup_proportional;
}

bool check_show_distance(int y)
{
  if (y < popup_distance)
  {
    return true;
  }

  if (interface_inv != null &&
    y < FloatToInt(IntToFloat(interface_inv.Height) * popup_proportional, eRoundNearest))
  {
      return true;
  }

  return false;
}

bool check_hide_distance(int y)
{
  if (interface_inv == null)
  {
    return y > popup_distance;
  }

  return y > popup_distance &&
    y > interface_inv.Height &&
    y > FloatToInt(IntToFloat(interface_inv.Height) * popup_proportional, eRoundNearest);
}

MouseButton check_reversed(MouseButton button)
{
  if (classic)
  {
    if (button == eMouseLeft) return eMouseRight;
    if (button == eMouseRight) return eMouseLeft;
    if (button == eMouseLeftInv) return eMouseRightInv;
    if (button == eMouseRightInv) return eMouseLeftInv;
  }

  return button;
}

function do_room_action(MouseButton button)
{
  if (GetLocationType(mouse.x, mouse.y) != eLocationNothing)
  {
    // clicked on something
    if (player.ActiveInventory == null)
    {
      if (button == check_reversed(eMouseLeft))
      {
        // left click to interact with target
        Room.ProcessClick(mouse.x, mouse.y, eModeInteract);
      }
      else
      {
        // right click to look at target
        Room.ProcessClick(mouse.x, mouse.y, eModeLookat);
      }
    }
    else
    {
      if (button == check_reversed(eMouseLeft))
      {
        // left click to use inventory on target
        Room.ProcessClick(mouse.x, mouse.y, eModeUseinv);
      }
      else
      {
        // right click to deselect inventory item
        player.ActiveInventory = null;
      }
    }
  }
  else
  {
    // click on nothing
    if (player.ActiveInventory == null)
    {
      // left click to walk
      Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
    }
    else
    {
      // right click to deselect inventory item
      player.ActiveInventory = null;
    }
  }
}

function do_inventory_action(MouseButton button, InventoryItem* item)
{
  if (button == check_reversed(eMouseLeftInv))
  {
    if (player.ActiveInventory == null)
    {
      if (item.GetProperty("InstantUse") == true)
      {
        // left click to instant use (if property is set)
        item.RunInteraction(eModeInteract);
      }
      else
      {
        // left click to set active inventory item
        player.ActiveInventory = item;
      }
    }
    else if (item.ID != player.ActiveInventory.ID)
    {
      // left click to use active inventory on another item
      item.RunInteraction(eModeUseinv);
    }
    else
    {
      // left click item on itself to deselect it
      player.ActiveInventory = null;
    }
  }
  else
  {
    // right click to look at inventory item
    item.RunInteraction(eModeLookat);
  }
}

//----------------------------------------------------------------------------------------------------
// on_mouse_click()
//----------------------------------------------------------------------------------------------------
function on_mouse_click(MouseButton button)
{
  // when mouse is clicked, text label is cleared
  if (action != null)
  {
    action.Text = "";
  }

  if (!IsGamePaused() && (button == eMouseLeft || button == eMouseRight))
  {
    do_room_action(button);
  }
  else if (button == eMouseLeftInv || button == eMouseRightInv)
  {
    // InventoryItem.GetAtScreenXY could return null here
    // so using game.inv_activated instead is a safer option
    do_inventory_action(button, inventory[game.inv_activated]);
  }
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute()
//----------------------------------------------------------------------------------------------------
function repeatedly_execute()
{
  // Inventory GUI:
  if (interface_inv == null)
  {
    // pass
  }
  else if (interface_inv.Visible && check_hide_distance(mouse.y))
  {
    interface_inv.Visible = false;
  }
  else if (!IsGamePaused() && !interface_inv.Visible && check_show_distance(mouse.y))
  {
    // make visible when the game is not paused and the cursor is within the popup position
    interface_inv.Visible = true;
  }

  // Action Text
  // We always display the name of what is under the mouse, with one exception:
  // IF the player has an inventory item selected and hovers over the same inventory item,
  // we display nothing to indicate that an item can not be used on itself
  if (player.ActiveInventory == null)
  {
    if (action != null && !IsGamePaused())
    {
      action.Text = Game.GetLocationName(mouse.x, mouse.y);
    }
  }
  else
  {
    InventoryItem *item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    if (action == null)
    {
      // pass
    }
    else if (item != null && item.ID == player.ActiveInventory.ID)
    {
      action.Text = "";
    }
    else
    {
      action.Text = Game.GetLocationName(mouse.x, mouse.y);
    }
  }
}

// handle clicks in the inventory area that are not on an inventory item
function on_event(EventType event, int data)
{
  if (event == eEventGUIMouseDown &&
    interface_inv != null &&
    data == interface_inv.ID &&
    InventoryItem.GetAtScreenXY(mouse.x, mouse.y) == null)
  {
    GUIControl* control = GUIControl.GetAtScreenXY(mouse.x, mouse.y);

    if (control == null || control.AsInvWindow == null)
    {
      // pass
    }
    else if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
  }
}
